
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Номенклатура 	= Параметры.Номенклатура;
	ОсновнойСклад 	= Параметры.ОсновнойСклад;
	ПолучитьДокументыВОтборахПоОстаткам();
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьДокументыВОтборахПоОстаткам()
	
	//оставить МассивНулевыхЯчеек только нулевые ячейки для СкладПользователя
	НачалоПериода 		= Дата (2018, 1, 1);
	МассивНулевыхЯчеек = Новый Массив;
	МассивНулевыхЯчеек.Добавить(Справочники.СкладскиеЯчейки.НайтиПоНаименованию("00000",,,ОсновнойСклад)); 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТоварыВЯчейкахОстатки.ВНаличииОстаток КАК Остаток,
		|	ТоварыВЯчейкахОстатки.Номенклатура КАК Номенклатура,
		|	ТоварыВЯчейкахОстатки.ВНаличииОстаток - ТоварыВЯчейкахОстатки.КОтборуОстаток КАК СвободныйОстаток,
		|	ТоварыВЯчейкахОстатки.Ячейка КАК Ячейка,
		|	ТоварыВЯчейкахОстатки.Ячейка.Владелец КАК ЯчейкаВладелец
		|ПОМЕСТИТЬ ОстатокНоменклатуры
		|ИЗ
		|	РегистрНакопления.ТоварыВЯчейках.Остатки КАК ТоварыВЯчейкахОстатки
		|ГДЕ
		|	ТоварыВЯчейкахОстатки.Номенклатура = &Номенклатура
		|	И ТоварыВЯчейкахОстатки.Ячейка В(&МассивНулевыхЯчеек)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ЯчейкаВладелец,
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СвободныеОстаткиОстатки.Номенклатура КАК Номенклатура,
		|	СвободныеОстаткиОстатки.Склад КАК Склад,
		|	СвободныеОстаткиОстатки.ВНаличииОстаток
		|ПОМЕСТИТЬ СвобОстатки
		|ИЗ
		|	РегистрНакопления.СвободныеОстатки.Остатки(
		|			,
		|			Склад = &СкладПользователя
		|				И Номенклатура = &Номенклатура) КАК СвободныеОстаткиОстатки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыКОтборуОстатки.Распоряжение.Распоряжение КАК СкладскоеЗадание,
		|	ТоварыКОтборуОстатки.Распоряжение.Распоряжение.Подготовлено КАК Подготовлено,
		|	ТоварыКОтборуОстатки.Распоряжение.Распоряжение.Партнер КАК Клиент,
		|	ОстатокНоменклатуры.ЯчейкаВладелец КАК Склад,
		|	ОстатокНоменклатуры.Остаток КАК Остаток,
		|	ОстатокНоменклатуры.СвободныйОстаток КАК СвободныйОстаток,
		|	ЕСТЬNULL(СвобОстатки.ВНаличииОстаток, 0) КАК ВНаличииОстаток,
		|	ТоварыКОтборуОстатки.КОтборуОстаток КАК КОтборуПоЗаданию,
		|	ТоварыКОтборуОстатки.ОтбираетсяОстаток КАК ОтбираетсяНаТСД,
		|	ТоварыКОтборуОстатки.ОтобраноОстаток КАК ОтобраноНаТСД
		|ИЗ
		|	ОстатокНоменклатуры КАК ОстатокНоменклатуры
		|		ЛЕВОЕ СОЕДИНЕНИЕ СвобОстатки КАК СвобОстатки
		|		ПО ОстатокНоменклатуры.Номенклатура = СвобОстатки.Номенклатура
		|			И ОстатокНоменклатуры.ЯчейкаВладелец = СвобОстатки.Склад
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыКОтбору.Остатки(, Номенклатура = &Номенклатура) КАК ТоварыКОтборуОстатки
		|		ПО ОстатокНоменклатуры.Номенклатура = ТоварыКОтборуОстатки.Номенклатура
		|			И ОстатокНоменклатуры.ЯчейкаВладелец = ТоварыКОтборуОстатки.Распоряжение.Склад
		|
		|УПОРЯДОЧИТЬ ПО
		|	Склад";
	
	Запрос.УстановитьПараметр("МассивНулевыхЯчеек", МассивНулевыхЯчеек);
	Запрос.УстановитьПараметр("СкладПользователя", ОсновнойСклад);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Отборы.Очистить();
	
	ЗначениеВРеквизитФормы(Запрос.Выполнить().Выгрузить(),"Отборы");
	
КонецПроцедуры
